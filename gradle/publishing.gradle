def artifactory_repo_key = System.getenv("ARTIFACTORY_REPO_KEY") != null ? System.getenv("ARTIFACTORY_REPO_KEY") : 'libs-snapshot-local'
def artifactory_username = System.getenv('ARTIFACTORY_USERNAME')
def artifactory_password = System.getenv('ARTIFACTORY_PASSWORD')

java {
    // Builds Javadoc into the published package as part of the 'assemble' task.
    withJavadocJar()

    // Builds sources into the published package as part of the 'assemble' task.
    withSourcesJar()
}
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(artifactoryPublish) &&
            (artifactory_username == null || artifactory_password == null)) {
        throw new GradleException("Set env vars ARTIFACTORY_USERNAME and ARTIFACTORY_PASSWORD to publish")
    }
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            groupId gradle.group
            artifactId project.name
            version gradle.version
            from components.java
        }
    }
}

artifactory {
    publish {
        contextUrl = 'https://broadinstitute.jfrog.io/broadinstitute/'
        repository {
            repoKey = "${artifactory_repo_key}"
            username = "${artifactory_username}"
            password = "${artifactory_password}"
        }
        defaults {
            publications(
                    'stairway',
                    'stairway-gcp')
        }
    }
}
