// Java conventions common for all of the code
plugins {
    id 'idea'
    id 'java'
    id 'java-library'
    id 'com.diffplug.spotless'
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    // For logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'

    // For testing
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.1'
    testRuntimeOnly group: 'org.codehaus.groovy', name: 'groovy', version: '3.0.13'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// Linter - Spotless configuration
spotless {
    java {
        googleJavaFormat()
        removeUnusedImports()
        targetExclude "${buildDir}/**"
    }
}
build.dependsOn spotlessApply

test {
    useJUnitPlatform {
        includeTags 'unit'
    }
}

// for scans
if (hasProperty("buildScan")) {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}
