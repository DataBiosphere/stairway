// Java conventions common for all of the code
plugins {
    id 'idea'
    id 'java'
    id 'java-library'
    id 'com.diffplug.spotless'
    id 'com.github.spotbugs'
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    ext {
        slf4j = '4.10.0'

        spotbugs = '4.7.3'

        groovy = '4.0.8'
        hamcrest = '2.2'
        junitJupiter = '5.9.2'
        logback = '1.4.5'
    }

    // Logging dependencies
    implementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4j}"

    // Spotbugs dependencies
    implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: "${spotbugs}"

    // Test dependencies
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version:  "${logback}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitJupiter}"
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitJupiter}"
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: "${hamcrest}"
    testRuntimeOnly group: 'org.apache.groovy', name: 'groovy', version: "${groovy}"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

dependencyLocking {
    lockAllConfigurations()
    lockMode = LockMode.STRICT
}

// Spotless configuration
spotless {
    groovyGradle {
        target "${rootDir}/**/*.gradle"
        indentWithSpaces()
    }
    java {
        googleJavaFormat()
        removeUnusedImports()
        targetExclude "${buildDir}/**"
    }
}
build.dependsOn spotlessApply

// Spotbugs configuration
spotbugs {
    effort = 'max'
}
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.html")
            stylesheet = 'fancy.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test.html")
            stylesheet = 'fancy.xsl'
        }
    }
}

test {
    useJUnitPlatform {
        includeTags 'unit'
    }
}
